buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.12.RELEASE")
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    compileJava.options.encoding = 'UTF-8'

    group = "com.wiiee.core"
    version = '0.1.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        manifest {
            attributes(
                    "Implementation-Title": "Gradle",
                    "Implementation-Version": version
            )
        }
    }


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':platform') {
    apply plugin: 'org.springframework.boot'

    bootRepackage {
        enabled = false
    }
}

project(':domain') {
    apply plugin: 'org.springframework.boot'

    bootRepackage {
        enabled = false
    }
}

project(':web') {
    apply plugin: 'org.springframework.boot'

    bootRepackage {
        enabled = false
    }
}

project(':sample') {
    apply plugin: 'org.springframework.boot'
}
